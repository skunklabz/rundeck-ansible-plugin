group 'com.batix.rundeck'
version = System.env.TRAVIS_TAG ?: "SNAPSHOT"

ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames = [
  'com.batix.rundeck.plugins.AnsibleResourceModelSourceFactory',
  'com.batix.rundeck.plugins.AnsibleNodeExecutor',
  'com.batix.rundeck.plugins.AnsibleFileCopier',
  'com.batix.rundeck.plugins.AnsiblePlaybookWorkflowStep',
  'com.batix.rundeck.plugins.AnsiblePlaybookInlineWorkflowStep',
  'com.batix.rundeck.plugins.AnsibleModuleWorkflowStep',
  'com.batix.rundeck.plugins.AnsiblePlaybookWorflowNodeStep',
  'com.batix.rundeck.plugins.AnsiblePlaybookInlineWorkflowNodeStep'
].join(',')

apply plugin: 'java'

sourceCompatibility = '1.7'

repositories {
  mavenCentral()
}

configurations {
  pluginLibs

  compile {
    extendsFrom pluginLibs
  }
}

dependencies {
  pluginLibs 'com.google.code.gson:gson:2.8.5'
  compile 'org.rundeck:rundeck-core:3.0.1-20180803'
}

task copyToLib(type: Copy) {
  into "$buildDir/output/lib"
  from configurations.pluginLibs
}

jar {
  from "$buildDir/output"
  manifest {
    def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
    attributes 'Rundeck-Plugin-Name' : 'Ansible Integration'
    attributes 'Rundeck-Plugin-Description' : 'This plugin brings basic Ansible support to Rundeck. It imports hosts from Ansible\'s inventory, including a bunch of facts, and can run modules and playbooks. There is also a node executor and file copier for your project.'
    attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.0.1+'
    attributes 'Rundeck-Plugin-Tags': 'java,node executor,resource model,workflow step,ansible'
    attributes 'Rundeck-Plugin-License': 'MIT'
    attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/Batix/rundeck-ansible-plugin'
    attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
    attributes 'Rundeck-Plugin-Author': 'David Kirstein'
    attributes 'Rundeck-Plugin-Classnames': pluginClassNames
    attributes 'Rundeck-Plugin-File-Version': version
    attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
    attributes 'Rundeck-Plugin-Archive': 'true'
    attributes 'Rundeck-Plugin-Libs': "${libList}"
    attributes 'Rundeck-Plugin-URL': 'https://github.com/Batix/rundeck-ansible-plugin'
  }
  dependsOn(copyToLib)
}
